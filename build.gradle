buildscript {
    repositories {
        maven { url = "https://files.minecraftforge.net/maven" }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}
plugins {
  id "com.wynprice.cursemaven" version "1.1.0"
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group = "knightminer"
archivesBaseName = "Inspirations"

// External properties
ext.configFile = file "build.properties"
configFile.withReader {
    // Load config.  It shall from now be referenced as simply config or project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}
configurations {
    deployerJars
}

version = "${config.minecraft_version}-${config.mod_version}"
//adds the build number to the end of the version string if on a build server
if (System.getenv().BUILD_NUMBER != null) {
	version += ".${System.getenv().BUILD_NUMBER}"
}

minecraft {
	mappings channel: 'snapshot', version: config.mappings_version

    accessTransformer project.file("src/main/resources/META-INF/accesstransformer.cfg")

     runs {
         client {
             workingDirectory project.file('run')

             // Recommended logging data for a userdev environment
             property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

             // Recommended logging level for the console
             property 'forge.logging.console.level', 'debug'

             mods {
                 inspirations {
                     source sourceSets.main
                 }
             }
         }

         server {
             workingDirectory project.file('run')

             // Recommended logging data for a userdev environment
             property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

             // Recommended logging level for the console
             property 'forge.logging.console.level', 'debug'

             mods {
                 inspirations {
                     source sourceSets.main
                 }
             }
         }

         data {
             workingDirectory project.file('run')

             // Recommended logging data for a userdev environment
             property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

             // Recommended logging level for the console
             property 'forge.logging.console.level', 'debug'

             args '--mod', 'inspirations', '--all', '--output', file('src/generated/resources/')

             mods {
                 inspirations {
                     source sourceSets.main
                 }
             }
         }
     }
}

sourceCompatibility = targetCompatibility = "1.8" // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

repositories {
    maven { // Mantle and JEI
        name 'DVS1 Maven FS'
        url 'https://dvs1.progwml6.com/files/maven'
    }
	maven { // TOP
        name 'tterrag maven'
        url "https://maven.tterrag.com/"
    }
	maven { // WAILA
        name "Tehnut Maven FS"
        url "https://tehnut.info/maven"
    }
	maven { // LLibrary
        url = "http://maven.mcmoddev.com"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${config.minecraft_version}-${config.forge_version}"
    
    // compile against the JEI API but do not include it at runtime
    compileOnly fg.deobf("mezz.jei:jei-${config.minecraft_version}:${config.jei_version}:api")
    // at runtime, use the full JEI jar
    runtimeOnly fg.deobf("mezz.jei:jei-${config.minecraft_version}:${config.jei_version}")

    compile fg.deobf("slimeknights.mantle:Mantle:${config.minecraft_version}-${config.mantle_version}")
    //deobfCompile "mcp.mobius.waila:Hwyla:${config.hwyla_version}"
    //deobfCompile("mcjty.theoneprobe:TheOneProbe-1.12:${config.probe_version}") {
    //    exclude group: 'cofh' // don't grab redstone flux too, don't use it
    //}
    //deobfCompile curse.resolve("leather-works", "2695934")
    //deobfCompile curse.resolve("tough-as-nails", "2644652")
	//deobfCompile "net.ilexiconn:llibrary:1.7.9-1.12.2"
    //deobfCompile curse.resolve("rats", "2732516")
    //deobfCompile curse.resolve("the-twilight-forest", "2618264")
}

jar {
    manifest {
        attributes([
            "Specification-Title": "Inspirations",
            "Specification-Vendor": "KnightMiner",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor": "KnightMiner",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

// because the normal output has been made to be obfuscated
task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact sourcesJar
        }
    }

    repositories {
        if (project.hasProperty('DEPLOY_DIR')) {
            maven { url DEPLOY_DIR }
        }
    }
}